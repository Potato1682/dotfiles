# hjkl bindings
zmodload -i zsh/complist

bindkey -M menuselect "^H" vi-backward-char
bindkey -M menuselect "^J" vi-down-line-or-history
bindkey -M menuselect "^K" vi-up-line-or-history
bindkey -M menuselect "^L" vi-forward-char

# Setup hooks in .zshrc.lazy to avoid instant prompt issues

# on JetBrains Terminal, the terminal is broken after running a command
if [[ "$TERMINAL_EMULATOR" != "JetBrains-JediTerm" ]] {
  add-zsh-hook -Uz precmd reset_broken_terminal
}

if [[ "$TERM" == (Eterm*|alacritty*|aterm*|foot*|gnome*|konsole*|kterm*|putty*|rxvt*|screen*|wezterm*|tmux*|xterm*) ]] {
  add-zsh-hook -Uz precmd xterm_title_precmd
  add-zsh-hook -Uz preexec xterm_title_preexec

  xterm_title_precmd
}

#
# Aliases
#

# Global aliases
alias -g UUID='$(uuidgen | tr -d \n)'
alias -g C="| cat"
alias -g G="| grep"
alias -g H="| head"
alias -g HC="| head | cat"
alias -g X="| xargs"

if (( $+commands[bat] )) {
  alias -g -- -h="-h 2>&1 | bat --language=help --style=plain --paging=never"
  alias -g -- --help="--help 2>&1 | bat --language=help --style=plain --paging=never"
}

# Extension based aliases
alias -s aria2="aria2c"
alias -s {pkg.tar.zst,pkg.tar.xz,pkg.tar.gz}="sudo pacman -U"

# Zsh features
autoload -Uz zmv
alias mmv="noglob zmv -W"

# Import shared rc
for file ($XDG_CONFIG_HOME/rc.d/*.sh) source "$file"

#
# Finalization
#

# zsh config compilation
_zcheck_compile() {
  if [[ -s ${1} && ( ! -s ${1}.zwc || ${1} -nt ${1}.zwc) ]] {
    zcompile ${1} 2>/dev/null
  }
}

_zcompile_profile() {
  zshrc="$ZDOTDIR/.zshrc"
  zshrc_lazy="$ZDOTDIR/.zshrc.lazy"
  zshenv="$ZDOTDIR/.zshenv"
  zprofile="$ZDOTDIR/.zprofile"

  _zcheck_compile "$zshrc"
  _zcheck_compile "$zshrc_lazy"
  _zcheck_compile "$zshenv"
  _zcheck_compile "$zprofile"

  p10k_config="$ZDOTDIR/.p10k.zsh"
  if [[ -f "$ZDOTDIR/.p10k.zsh" ]] {
    _zcheck_compile "$p10k_config"
  }
}

_zcompile_profile

